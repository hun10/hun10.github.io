<!DOCTYPE html>
<html>
    <head>
        <meta name="viewport" content="width=320">
        <title>Yacc and Lex Playground</title>
        <style>
textarea {
  font-family: monospace;
}

.error {
  color: red;
}

.warning {
  color: orange;
}

.good {
  color: green;
}
        </style>
        <script src="jquery-1.11.2.js"></script>
        <script src="jison.js"></script>
        <script src="yacclex.js"></script>
    </head>
    <body>
        <h1>Yacc and Lex Playground</h1>
        <p>Uses <a href="https://github.com/zaach/jison">Jison</a> parser generator.</p>
        <div>
            <textarea id="grammar" rows="20" cols="80">%lex
%%

\s+ ;

halt                  return 'HALT';

read                  return 'READ';
write                 return 'WRITE';
store                 return 'STORE';
load                  return 'LOAD';

add                   return 'ADD';
mul                   return 'MUL';
sub                   return 'SUB';
div                   return 'DIV';

jmp                   return 'JMP';
jz                    return 'JZ';
jgtz                  return 'JGTZ';

':'                   return ':';
'='                   return '=';

[a-z]+[0-9]*          return 'LABEL';
0|([1-9][0-9]*)       return 'NUMBER';

<<EOF>>               return 'EOF';
.                     return 'INVALID';

/lex

%start exp

%%

exp : prog EOF  { return process(); }
    ;

prog : prog piece
     | piece
     ;

piece : LABEL ':' { labels[$1] = code.length; }
      | command   { code.push($1); }
      ;

command : WRITE arg      { $$ = function() { $output += $2() + "\n"; }; }
        | READ  NUMBER   { $$ = function() { regs[Number($2)] = readInput(); }; }
        | HALT           { $$ = function() { codepointer = -2; }; }
        | STORE NUMBER   { $$ = function() { regs[Number($2)] = regs[0]; }; }
        | LOAD  arg      { $$ = function() { regs[0] = $2(); }; }
        | ADD   arg      { $$ = function() { regs[0] = regs[0] + $2(); }; }
        | MUL   arg      { $$ = function() { regs[0] = regs[0] * $2(); }; }
        | SUB   arg      { $$ = function() { regs[0] = Math.max(0, regs[0] - $2()); }; }
        | DIV   arg      { $$ = function() { regs[0] = Math.floor(regs[0] / $2()); }; }
        | JMP   label    { $$ = function() { codepointer = $2(); }; }
        | JZ    label    { $$ = function() { if (regs[0] == 0) codepointer = $2(); }; }
        | JGTZ  label    { $$ = function() { if (regs[0] > 0) codepointer = $2(); }; }
        ;

arg : nonconst   { $$ = $1; }
    | '=' NUMBER { $$ = function() { return Number($2); }; }
    ;

nonconst : NUMBER { $$ = function() { return regs[Number($1)]; }; }
         ;

label : LABEL { $$ = function() { return labels[$1]; }; }
      ;

%%

var labels = [];
var code = [];
var regs = [0];
var codepointer = 0;
var run = true;
var $output = "";

var $raminp = $("#ramInput").val().match(/(\d+)/g);

function readInput() {
  var v = $raminp.shift(1);
  if (v) {
    return v;
  } else {
    throw new Error("No number to read.");
  }
}

function process() {
  while (run) {
    var cmd = code[codepointer++];
    if (!cmd) break;
    cmd();
  }
  return $output;
}</textarea>
            <p>
                <button id="generate">Generate Parser</button>
            </p>
            <pre id="generatorFeedback">
            </pre>
        </div>
        <div>
            <textarea id="input" rows="10" cols="40">
load =1
store 2

read 1

loop:
load 1
jz exit

load 2
mul =2
store 2

load 1
sub =1
store 1

jmp loop

exit:
write 2
halt</textarea>
            <textarea id="ramInput" rows="10" cols="40">
8</textarea>
            <p>
                <button id="parse">Parse</button>
            </p>
        </div>
        <pre id="output">
        </pre>
    </body>
</html>
